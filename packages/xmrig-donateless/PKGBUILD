# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='xmrig'
pkgname='xmrig-donateless'
pkgver="6.18.0"
pkgrel=2
provides=("xmrig=${pkgver}")
conflicts=('xmrig')
pkgdesc='RandomX, KawPow, CryptoNight, AstroBWT and GhostRider unified CPU/GPU miner and RandomX benchmark - No Donate Version'
arch=('x86_64')
url="https://github.com/xmrig/xmrig"
license=('MIT')
depends=('libuv' 'openssl' 'hwloc')
makedepends=('cmake' 'sed')
optdepends=('ocl-icd: AMD GPU mining'
            'opencl-headers: AMD GPU mining'
            'xmrig-cuda: NVIDIA CUDA mining'
            'cuda: NVIDIA GPU mining')
source=("${url}/archive/v${pkgver}/${_pkgname}-v${pkgver}.tar.gz"
        "${_pkgname}@.service"
        "${_pkgname}.sysusers")
sha256sums=('4531a31c0c095fcae18fdef0157f1e2a6694408abbcff6789c8f3cd6ab2c3ca0'
            'bbc54b04a7da5ea473eb7d976307eb995fbfd96f0d012f1553b8c487182d9fb3'
            'd8f499302fb2b642fe02586c81c410a299e0a6e133aef1cc1c783bcdcb3f44f6')

prepare() {
  mkdir "${_pkgname}-${pkgver}/build"
  cd "${_pkgname}-${pkgver}"
  sed -i 's/constexpr const int kDefaultDonateLevel = 1;/constexpr const int kDefaultDonateLevel = 0;/g' src/donate.h
  sed -i 's/constexpr const int kMinimumDonateLevel = 1;/constexpr const int kMinimumDonateLevel = 0;/g' src/donate.h
}

build() {
  cd "${_pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm 775 "build/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm 644 "src/config.json" "${pkgdir}/etc/${_pkgname}/xmrig.conf"
  install -Dm 644 "${srcdir}/${_pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${_pkgname}@.service"
  install -Dm 0644 "${srcdir}/${_pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${_pkgname}.conf"
}
